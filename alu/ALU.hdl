// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a= x, b= false, sel= zx, out= newX);
    Not16(in= newX, out= notNewX);
    Mux16(a= newX, b= notNewX, sel= nx, out= finalX);

    Mux16(a= y, b= false, sel= zy, out= newY);
    Not16(in= newY, out= notNewY);
    Mux16(a= newY, b= notNewY, sel= ny, out= finalY);

    Add16(a = finalX, b = finalY, out = sumXY);
    And16(a= finalX, b= finalY, out= xAndy);
    Mux16(a= xAndy, b= sumXY, sel= f, out= xfy);

    Not16(in= xfy, out= notxfy);
    Mux16(a= xfy, b= notxfy, sel= no, out= finalOut);

    Mux16(a= finalOut, b= false, sel= false, out[15]= ng);

    Mux16(a= finalOut, b= false, sel= false, out[0..7]= first);
    Mux16(a= finalOut, b= false, sel= false, out[8..15]= second);
    Or8Way(in= first, out= isFirstNotZero);
    Or8Way(in= second, out= isSecondNotZero);
    Or(a= isFirstNotZero, b= isSecondNotZero, out= isNotZero);
    Not(in= isNotZero, out= zr);
    
    Mux16(a= finalOut, b= false, sel= false, out= out);

    
    
}